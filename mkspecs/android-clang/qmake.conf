# qmake configuration for building with android-g++
MAKEFILE_GENERATOR      = UNIX
QMAKE_PLATFORM          = android
QMAKE_COMPILER          = gcc clang llvm

CONFIG                 += android_install unversioned_soname unversioned_libname android_deployment_settings

include(../common/linux.conf)
include(../common/clang.conf)
include(../common/android-base-head.conf)

NDK_LLVM_PATH = $$NDK_ROOT/toolchains/llvm/prebuilt/$$NDK_HOST
QMAKE_CC      = $$NDK_LLVM_PATH/bin/clang
QMAKE_CXX     = $$NDK_LLVM_PATH/bin/clang++

equals(ANDROID_TARGET_ARCH, armeabi-v7a): \
    QMAKE_CFLAGS = -target armv7-none-linux-androideabi
else: equals(ANDROID_TARGET_ARCH, armeabi): \
    QMAKE_CFLAGS = -target armv5te-none-linux-androideabi
else: equals(ANDROID_TARGET_ARCH, arm64-v8a): \
    QMAKE_CFLAGS = -target aarch64-none-linux-android
else: equals(ANDROID_TARGET_ARCH, x86): \
    QMAKE_CFLAGS = -target i686-none-linux-android
else: equals(ANDROID_TARGET_ARCH, x86_64): \
    QMAKE_CFLAGS = -target x86_64-none-linux-android
else: equals(ANDROID_TARGET_ARCH, mips): \
    QMAKE_CFLAGS += -target mipsel-none-linux-android
else: equals(ANDROID_TARGET_ARCH, mips64): \
    QMAKE_CFLAGS = -target mips64el-none-linux-android

QMAKE_LINK = $$QMAKE_CXX $$QMAKE_CFLAGS -gcc-toolchain $$NDK_TOOLCHAIN_PATH

include(../common/android-base-tail.conf)

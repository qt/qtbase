#
# qmake configuration for iphone-device-g++
#
include(../../common/mac.conf)
include(../../common/gcc-base-macx.conf)
include(../../common/g++-macx.conf)

MAKEFILE_GENERATOR	= UNIX
TEMPLATE		= app
CONFIG			+= qt warn_on release app_bundle incremental global_init_link_order lib_version_first plugin_no_soname link_prl
QT			+= core gui
QMAKE_INCREMENTAL_STYLE = sublib

# Do not compile a few things
DEFINES += QT_NO_AUDIO_BACKEND QT_NO_NETWORKPROXY QT_NO_FILESYSTEMWATCHER

# You may need to change this to point to the iOS SDK you want to use.
QMAKE_IOS_DEV_PATH = /Developer/Platforms/iPhoneOS.platform/Developer
QMAKE_IOS_SDK = $$QMAKE_IOS_DEV_PATH/SDKs/iPhoneOS4.3.sdk
DEFINES += __IPHONE_OS_VERSION_MIN_REQUIRED=40200

#clear
QMAKE_MACOSX_DEPLOYMENT_TARGET = 

QMAKE_LIBS_OPENGL_ES1 += -framework OpenGLES
QMAKE_LIBS_OPENGL_ES2 += -framework OpenGLES

# TARGET_PLATFORM = ios
QMAKE_CC         = /Developer/Platforms/iPhoneOS.platform/Developer/usr/bin/gcc-4.2
QMAKE_CXX        = /Developer/Platforms/iPhoneOS.platform/Developer/usr/bin/g++-4.2
QMAKE_LINK              = $$QMAKE_CXX
QMAKE_LINK_SHLIB        = $$QMAKE_CXX

QMAKE_CFLAGS           += -arch armv7 -marm -isysroot $$QMAKE_IOS_SDK -fmessage-length=0 -fexceptions -miphoneos-version-min=4.2
QMAKE_CXXFLAGS	        += $$QMAKE_CFLAGS
QMAKE_OBJECTIVE_CFLAGS += -arch armv7 -marm -isysroot $$QMAKE_IOS_SDK -fmessage-length=0 -fexceptions -miphoneos-version-min=4.2 -fobjc-abi-version=2 -fobjc-legacy-dispatch
QMAKE_LFLAGS           += -arch armv7 -marm -miphoneos-version-min=4.2 -Wl,-syslibroot,$$QMAKE_IOS_SDK

QMAKE_INCDIR_OPENGL	= 
QMAKE_LIBS_OPENGL	=
QMAKE_LIBS_OPENGL_QT	=

#QMAKE_RESOURCE		=
QMAKE_FIX_RPATH = $$QMAKE_IOS_DEV_PATH/usr/bin/install_name_tool -id 
QMAKE_AR        = $$QMAKE_IOS_DEV_PATH/usr/bin/ar cq
QMAKE_RANLIB    = $$QMAKE_IOS_DEV_PATH/usr/bin/ranlib -s

load(qt_config)

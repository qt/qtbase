#
# qmake configuration for building with nacl-emscripten
#

QMAKE_INCDIR += $$(EMSCRIPTEN)/system/include

include(../../common/unix.conf)
include(../../common/gcc-base-unix.conf)
include(../../common/g++-unix.conf)
include(../../common/nacl/nacl-base.conf)
include(../../common/nacl/nacl-newlib-static.conf)

# emscripten

QMAKE_CC                = emcc
QMAKE_CXX               = em++
QMAKE_LINK              = em++
QMAKE_LINK_SHLIB        = em++
QMAKE_AR                = emar r

CONFIG *= emscripten

# Uset default NaCl setting
QMAKE_LIBS_OPENGL_ES2=

emscripten_pthreads {
    CONFIG += emscripten_pthreads
    QMAKE_CXXFLAGS += -s USE_PTHREADS=1 \

    QMAKE_LFLAGS += -s USE_PTHREADS=1 \
        -s PTHREAD_POOL_SIZE=4\

}

QMAKE_LFLAGS += -Wl

# Add link-time pepper platform plugin javascript dependencies and
# required emscripten linker options.
PEPPER_JS_PATH=$$PWD/../../../src/plugins/platforms/pepper/3rdparty/pepper.js

QMAKE_LFLAGS += \
    -s RESERVED_FUNCTION_POINTERS=400\
    -s TOTAL_MEMORY=1280000000\
    -s FULL_ES2=1 \
    -s INCLUDE_FULL_LIBRARY=1 \
    -s EXPORTED_FUNCTIONS=\"[\'_DoPostMessage\', \'_DoChangeView\', \'_DoChangeFocus\', \'_NativeCreateInstance\', \'_HandleInputEvent\']\"\
    --pre-js $${PEPPER_JS_PATH}/ppapi_preamble.js\
    --pre-js $${PEPPER_JS_PATH}/base.js\
    --pre-js $${PEPPER_JS_PATH}/audio.js\
    --pre-js $${PEPPER_JS_PATH}/file.js\
    --pre-js $${PEPPER_JS_PATH}/gles.js\
    --pre-js $${PEPPER_JS_PATH}/gles_ext.js\
    --pre-js $${PEPPER_JS_PATH}/graphics_2d.js\
    --pre-js $${PEPPER_JS_PATH}/graphics_3d.js\
    --pre-js $${PEPPER_JS_PATH}/input_events.js\
    --pre-js $${PEPPER_JS_PATH}/mouse_lock.js\
    --pre-js $${PEPPER_JS_PATH}/url_loader.js\
    --pre-js $${PEPPER_JS_PATH}/view.js\
    --pre-js $${PEPPER_JS_PATH}/web_socket.js\

load(qt_config)
